/*
 * Copyright 2003-2007 (c) Live Media Pty Ltd. <argot@einet.com.au> 
 *
 * This software is licensed under the Argot Public License 
 * which may be found in the file LICENSE distributed 
 * with this software.
 *
 * More information about this license can be found at
 * http://www.einet.com.au/License
 * 
 * The Developer of this software is Live Media Pty Ltd,
 * PO Box 4591, Melbourne 3001, Australia.  The license is subject 
 * to the law of Victoria, Australia, and subject to exclusive 
 * jurisdiction of the Victorian courts.
 */
 
 /** 
 * This is the network virtual machine types.  
 *
 * 
 *
 */

!import empty;
!import uint8;
!import uint16;
!import uint32;
!import int32;
!import u8ascii;

!import u32binary;

!import meta.identified;
!import meta.array;
!import meta.reference;
!import meta.sequence;
!import meta.name;
!import meta.expression;
!import meta.definition;

!import remote.location;
!import remote.object;
!import remote.interface;
!import remote.exception;

(dictionary.source [

/*
 * A stack is an array of items.  In future this could be expanded
 * to array( U16, contained( U16 meta.identified( meta.any ) ) )
 * this would allow each item on the stack to be moved around without
 * actually understanding what the item is.
 */

(meta.structure meta.name:"nvm.heap" (meta.sequence [
	(meta.array 
		(meta.reference #uint16)
		(meta.identified u8ascii:"object")
	)]))
	

(meta.structure meta.name:"nvm.instruction" (meta.abstract))

(meta.structure meta.name:"nvm.instruction.load_crl_host" (meta.sequence []))
(meta.structure meta.name:"nvm.instruction.load_crl_object" (meta.sequence []))
(meta.structure meta.name:"nvm.instruction.load_heap_object" (meta.sequence [
		(meta.tag u8ascii:"heapIndex" (meta.reference #uint16))]))
(meta.structure meta.name:"nvm.instruction.named_method_call" (meta.sequence [
		(meta.tag u8ascii:"methodName" (meta.reference #u8ascii))
		(meta.tag u8ascii:"typeId"
			(meta.array
				(meta.reference #uint8)
				(meta.reference #uint16)
			)
		)]))
(meta.structure meta.name:"nvm.instruction.return" (meta.sequence []))
(meta.structure meta.name:"nvm.instruction.store_heap_object" (meta.sequence [
		(meta.tag u8ascii:"heapIndex" (meta.reference #uint16))]))
(meta.structure meta.name:"nvm.instruction.to_host" (meta.sequence []))

(meta.map #nvm.instruction #nvm.instruction.load_crl_host)
(meta.map #nvm.instruction #nvm.instruction.to_host)
(meta.map #nvm.instruction #nvm.instruction.load_crl_object)
(meta.map #nvm.instruction #nvm.instruction.load_heap_object)
(meta.map #nvm.instruction #nvm.instruction.named_method_call)
(meta.map #nvm.instruction #nvm.instruction.return)
(meta.map #nvm.instruction #nvm.instruction.store_heap_object)

(meta.structure meta.name:"nvm.instructions" (meta.sequence [
	(meta.array
		(meta.reference #uint16)
		(meta.reference #nvm.instruction)
	)
]))

/*
 * This virtual machine state includes instructions, heap, program counter
 * and exception state.  
 */

(meta.structure meta.name:"nvm.state" (meta.sequence [
	(meta.tag u8ascii:"programCounter" (meta.reference #uint16))
	(meta.tag u8ascii:"instructions" (meta.reference #nvm.instructions))
	(meta.tag u8ascii:"heap" (meta.reference #nvm.heap))
	(meta.tag u8ascii:"exception" (meta.reference #remote.exception))
]))


(meta.structure meta.name:"nvm.host" (remote.interface))

(meta.structure meta.name:"nvm.host.process"
	(remote.method #nvm.host u8ascii:"process"
		[ (remote.parameter #nvm.state u8ascii:"nvmState" ) ]
		[ (remote.parameter #nvm.state u8ascii:"nvmState" ) ]
		[ #remote.exception ]
	)
)

(meta.structure meta.name:"nvm.host.getobject"
	(remote.method #nvm.host u8ascii:"getObject"
		[ (remote.parameter #remote.location u8ascii:"metaLocation" ) ]
		[ (remote.parameter #remote.object u8ascii:"metaObject" ) ]
		[ #remote.exception ]
	)
)

])